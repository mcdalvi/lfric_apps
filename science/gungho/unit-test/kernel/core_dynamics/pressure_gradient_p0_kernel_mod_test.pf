!-----------------------------------------------------------------------------
! (C) Crown copyright 2024 Met Office. All rights reserved.
! For further details please refer to the file COPYRIGHT.txt
! which you should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test of pressure gradient kernel for lowest order elements
!>
module pressure_gradient_p0_kernel_mod_test

  use constants_mod, only : i_def, r_def
  use funit

  implicit none

  private
  public :: test_all

contains

  @test( )
  subroutine test_all( )

    use pressure_gradient_p0_kernel_mod, only : pressure_gradient_p0_code

    implicit none

    real(r_def), parameter :: tol = 1.0e-12_r_def

    ! Mesh
    integer(i_def), parameter :: nlayers = 3
    integer(i_def), parameter :: ncells = 5
    ! Spaces
    integer(i_def), parameter :: ndf_w2 = 6
    integer(i_def), parameter :: undf_w2 = 4*nlayers + nlayers+1
    integer(i_def), parameter :: ndf_w3 = 1
    integer(i_def), parameter :: undf_w3 = ndf_w3*nlayers
    integer(i_def), parameter :: ndf_wt = 2
    integer(i_def), parameter :: undf_wt = (nlayers+1)*ncells

    ! Maps
    integer(i_def), dimension(ndf_w2)  :: map_w2
    integer(i_def), dimension(ndf_wt)  :: map_wt
    integer(i_def), dimension(ndf_w3)  :: map_w3

    ! Stencil Maps
    integer(i_def), parameter    :: smap_wt_max = 2
    integer(i_def), dimension(4) :: smap_wt_size
    integer(i_def), dimension(ndf_wt, smap_wt_max, 4) :: smap_wt

    ! Fields
    real(r_def), dimension(undf_w2) :: rhs
    real(r_def), dimension(undf_w3) :: exner
    real(r_def), dimension(undf_w3) :: geopotential
    real(r_def), dimension(undf_wt) :: theta_v

    ! Scalars
    real(r_def), parameter :: cp = 1000.0_r_def

    integer(i_def) :: d
    real(r_def) :: answer

    ! Create dof maps
    map_w3(1) = 1
    map_wt(:) = (/ 1, 2 /)
    map_w2(:) = (/ 1, nlayers+1, 2*nlayers+1, 3*nlayers+1, 4*nlayers+1, 4*nlayers+2 /)

    smap_wt_size(:) = 2
    do d = 1, 4
      smap_wt(:, 1, d) = map_wt
      smap_wt(:, 2, d) = map_wt + d*(nlayers+1)
    end do

    ! Create field data

    rhs(:) = 0.0_r_def
    ! dp/dz = 1
    exner(:) = (/ 1.0_r_def, 2.0_r_def, 3.0_r_def /)
    ! dphi/dz = 2
    geopotential(:) = (/ 10.0_r_def, 12.0_r_def, 14.0_r_def /)
    ! dt/dz = 100
    ! dt/dx = 10
    do d = 1, nlayers+1
      theta_v(d) = 300.0_r_def + 100.0_r_def*(d-1)
      theta_v(d + 1*(nlayers+1)) = theta_v(d) - 10.0_r_def
      theta_v(d + 2*(nlayers+1)) = theta_v(d) - 10.0_r_def
      theta_v(d + 3*(nlayers+1)) = theta_v(d) + 10.0_r_def
      theta_v(d + 4*(nlayers+1)) = theta_v(d) + 10.0_r_def
    end do

    call pressure_gradient_p0_code( nlayers,                  &
                                    rhs, exner, geopotential, &
                                    theta_v,                  &
                                    smap_wt_size,             &
                                    smap_wt_max,              &
                                    smap_wt,                  &
                                    cp,                       &
                                    ndf_w2, undf_w2, map_w2,  &
                                    ndf_w3, undf_w3, map_w3,  &
                                    ndf_wt, undf_wt, map_wt )

    ! Horizontal term: cp*theta_av*dp/dx + dphi/dx
    answer = -cp*345.0_r_def*exner(1) - geopotential(1)
    @assertEqual(answer, rhs(map_w2(1)), tol)
    answer =  cp*355.0_r_def*exner(1) + geopotential(1)
    @assertEqual(answer, rhs(map_w2(3)), tol)

    ! Vertical term: cp*theta_av*dp/dz + dphi/dz
    answer = -cp*400.0_r_def*(exner(2)-exner(1)) - (geopotential(2) - geopotential(1))
    @assertEqual(answer, rhs(map_w2(6)), tol)

    ! Check if we have a lam domain with no neighbours that the horizontal values are
    ! zero
    ! Reset the rhs so that the computation is independent of the previous test
    rhs(:) = 0.0_r_def
    smap_wt_size(:) = 1
    call pressure_gradient_p0_code( nlayers,                  &
                                    rhs, exner, geopotential, &
                                    theta_v,                  &
                                    smap_wt_size,             &
                                    smap_wt_max,              &
                                    smap_wt,                  &
                                    cp,                       &
                                    ndf_w2, undf_w2, map_w2,  &
                                    ndf_w3, undf_w3, map_w3,  &
                                    ndf_wt, undf_wt, map_wt )

    ! Check horizontal components are zero
    do d = 1, 4
      @assertEqual(0.0_r_def, rhs(map_w2(d)), tol)
    end do
    ! Vertical term: cp*theta_av*dp/dz + dphi/dz
    answer = -cp*400.0_r_def*(exner(2)-exner(1)) - (geopotential(2) - geopotential(1))
    @assertEqual(answer, rhs(map_w2(6)), tol)

  end subroutine test_all

end module pressure_gradient_p0_kernel_mod_test
