! *****************************COPYRIGHT*******************************
! (C) Crown copyright Met Office. All rights reserved.
! For further details please refer to the file COPYRIGHT.txt
! which you should have received as part of this distribution.
! *****************************COPYRIGHT*******************************

module wtrac_pc2_phase_chg_mod

use um_types, only: real_umphys

implicit none

! Description:
!  Update water tracers for PC2 phase change
!
! Method:
!   Update water tracers using the amount of water that has condensed
!   together with the ratio of water tracer to water of the vapour.
!
! Code Owner:  Please refer to the UM file CodeOwners.txt
! This file belongs in section: Water_Tracers
!
! Code Description:
!   Language: FORTRAN 90
!   This code is written to UMDP3 programming standards.

character(len=*), parameter, private :: ModuleName = 'WTRAC_PC2_PHASE_CHG_MOD'
contains

! Subroutine Interface:
subroutine wtrac_pc2_phase_chg(qdims, q_new_3d, qcl_new_3d, call_routine,      &
                               wtrac, wtrac_as, wtrac_mp)

use atm_fields_bounds_mod,   only: array_dims, tdims
use free_tracers_inputs_mod, only: n_wtrac
use nlsizes_namelist_mod,    only: row_length, rows, model_levels
use water_tracers_mod,       only: wtrac_type
use wtrac_atm_step_mod,      only: atm_step_wtrac_type
use wtrac_all_phase_chg_mod, only: wtrac_all_phase_chg
use wtrac_pc2_mod,           only: wtrac_pc2, wtrac_pc2_store_dealloc
use wtrac_mphys_mod,         only: mp_wtrac_type

use Ereport_mod,             only: ereport
use errormessagelength_mod,  only: errormessagelength

use yomhook,                 only: lhook, dr_hook
use parkind1,                only: jprb, jpim

implicit none

! Subroutine arguments

type(array_dims), intent(in) :: qdims         ! Input q field dims

! q after PC2 phase change  (NOTE NO HALOS and 3D fields)
real(kind=real_umphys), intent(in) :: q_new_3d(qdims%i_start:qdims%i_end,      &
                                      qdims%j_start:qdims%j_end,               &
                                      1:qdims%k_end)

! qcl after PC2 phase change (NOTE NO HALOS and 3D fields)
real(kind=real_umphys), intent(in) :: qcl_new_3d(qdims%i_start:qdims%i_end,    &
                                      qdims%j_start:qdims%j_end,               &
                                      1:qdims%k_end)

character(len=*), intent(in) :: call_routine   ! Calling routine

! Water tracer structures containing water tracer fields prior to phase
! change: either
! a) the main prognostic fields
type(wtrac_type), optional, intent(in out) :: wtrac(n_wtrac)
! or b) the working arrays in the microphysics
type(mp_wtrac_type), optional, intent(in out):: wtrac_mp(n_wtrac)
! or c) the top_level working arrays (which include the 'star'/'inc' fields)
type(atm_step_wtrac_type), optional, intent(in out) :: wtrac_as(n_wtrac)


! Local parameters

integer :: i,j,k,l,i_wt          ! Loop counters

integer :: ErrorStatus

logical :: l_limit_chg           ! If T, limit phase change to ensure the
                                 ! source water tracer value stays >=0

! Working arrays (Note order of array dimensions i.e. nx*ny,n_wtrac,nz)
real(kind=real_umphys) :: q_wtrac(row_length*rows, n_wtrac, model_levels)
real(kind=real_umphys) :: qcl_wtrac(row_length*rows, n_wtrac, model_levels)

! 1D working arrays
real(kind=real_umphys) :: q_old(row_length*rows)
real(kind=real_umphys) :: qcl_old(row_length*rows)
real(kind=real_umphys) :: q_new(row_length*rows)
real(kind=real_umphys) :: qcl_new(row_length*rows)
real(kind=real_umphys) :: qcl_change(row_length*rows)

! Water tracer q chg
real(kind=real_umphys) :: qchange_wtrac(row_length*rows,n_wtrac,model_levels)

character(len=errormessagelength) :: cmessage

integer(kind=jpim), parameter :: zhook_in  = 0
integer(kind=jpim), parameter :: zhook_out = 1
real   (kind=jprb)            :: zhook_handle

character(len=*), parameter :: RoutineName='WTRAC_PC2_PHASE_CHG'

! Notes on different options:
!
! 'pc2_initiate' or 'pc2_checks2' - updates main prognostic fields.
!                                   wtrac must be present.
!
! 'pc2_hpt_press' - (=pc2_homog_plus_turb called from pc2_pressure_forcing)
!                    updates main prognostic fields. wtrac must be present.
!
! 'pc2_hpt_rad' - (=pc2_homog_plus_turb called from rad_ctl)
!                updates increment fields. wtrac and wtrac_as must be present.
!
! 'mphys_turb' - update increment fields due to mixed phase cloud generated by
!                turbulent processes in microphysics scheme. wtrac_mp and
!                wtrac_as must be present.
!
! 'pc2_bl_forced_cu' - update working arrays due to forced Cumulus clouds
!                      at top of well-mixed boundary layer.  wtrac_as must be
!                      present.

! End of header

if (lhook) call dr_hook(ModuleName//':'//RoutineName,zhook_in,zhook_handle)

ErrorStatus = 0
! Check that the correct fields are present.  If not, abort model.
select case (call_routine)
case ('pc2_initiate', 'pc2_checks2', 'pc2_hpt_press')
  if (.not. present(wtrac)) then
    ErrorStatus = 100
    cmessage =  'Water tracer structure wtrac needs to be present'
  end if
case ('pc2_hpt_rad')
  if (.not. present(wtrac) .or. .not. present(wtrac_as)) then
    ErrorStatus = 100
    cmessage =  'Water tracer structures wtrac and wtrac_as need to be present'
  end if
case ('mphys_turb')
  if (.not. present(wtrac_mp) .or. .not. present(wtrac_as)) then
    ErrorStatus = 100
    cmessage =                                                                 &
      'Water tracer structures wtrac_mp and wtrac_as need to be present'
  end if
case ('pc2_bl_forced_cu')
  if (.not. present(wtrac_as)) then
    ErrorStatus = 100
    cmessage =  'Water tracer structure wtrac_as needs to be present'
  end if
case DEFAULT
  ErrorStatus = 100
  cmessage = 'Incorrect call_routine setting'
end select

call ereport(RoutineName, ErrorStatus, cmessage)

! Set up logical which controls whether the amount of water tracer moving
! during a phase change is limited to the amount of source water tracer
if (call_routine == 'pc2_checks2') then
  ! pc2_checks2 does not apply this limit as it can create negative qcl
  ! values which are then corrected in pc2_checks.
  l_limit_chg = .false.
else
  l_limit_chg = .true.
end if

!$OMP PARALLEL DEFAULT(SHARED) private(i,j,k,l,i_wt,q_old,qcl_old,q_new,       &
!$OMP qcl_new,qcl_change)

if (call_routine == 'pc2_initiate') then    ! Calculate phase change amount
!$OMP do SCHEDULE(STATIC)
  do k = 1, tdims%k_end
    do j = tdims%j_start, tdims%j_end            ! No halos
      do i = tdims%i_start, tdims%i_end
        wtrac_pc2%q_cond(i,j,k) = qcl_new_3d(i,j,k) - wtrac_pc2%qcl_old(i,j,k)
      end do
    end do
  end do
!$OMP end do
end if

! Set up water tracer working arrays

select case(call_routine)

case ('mphys_turb')
!$OMP do SCHEDULE(STATIC)
  do k = 1, tdims%k_end
    do j = tdims%j_start, tdims%j_end            ! No halos
      do i = tdims%i_start, tdims%i_end
        l = (j-1)*row_length + i
        do i_wt = 1, n_wtrac
          q_wtrac(l,i_wt,k)   = wtrac_mp(i_wt)%q(i,j,k)
          qcl_wtrac(l,i_wt,k) = wtrac_mp(i_wt)%qcl(i,j,k)
        end do
      end do
    end do
  end do
!$OMP end do

case ('pc2_bl_forced_cu')
!$OMP do SCHEDULE(STATIC)
  do k = 1, tdims%k_end
    do j = tdims%j_start, tdims%j_end            ! No halos
      do i = tdims%i_start, tdims%i_end
        l = (j-1)*row_length + i
        do i_wt = 1, n_wtrac
          q_wtrac(l,i_wt,k)   = wtrac_as(i_wt)%q_star(i,j,k)
          qcl_wtrac(l,i_wt,k) = wtrac_as(i_wt)%qcl_star(i,j,k)
        end do
      end do
    end do
  end do
!$OMP end do

case ('pc2_initiate','pc2_checks2','pc2_hpt_press','pc2_hpt_rad')
!$OMP do SCHEDULE(STATIC)
  do k = 1, tdims%k_end
    do j = tdims%j_start, tdims%j_end            ! No halos
      do i = tdims%i_start, tdims%i_end
        l = (j-1)*row_length + i
        do i_wt = 1, n_wtrac
          q_wtrac(l,i_wt,k)   = wtrac(i_wt)%q(i,j,k)
          qcl_wtrac(l,i_wt,k) = wtrac(i_wt)%qcl(i,j,k)
        end do
      end do
    end do
  end do
!$OMP end do

case DEFAULT
  ErrorStatus = 100
  cmessage =                                                                   &
    'Working arrays have not been set due to invalid call_routine name'
  call ereport(RoutineName, ErrorStatus, cmessage)
end select

! Calculate new water tracer values after phase change

!$OMP do SCHEDULE(STATIC)
do k = 1, tdims%k_end

  ! Put fields into 1D arrays for call to wtrac_all_phase_chg
  do j = tdims%j_start, tdims%j_end            ! No halos
    do i = tdims%i_start, tdims%i_end
      l = (j-1)*row_length + i
      q_old(l) = wtrac_pc2%q_old(i,j,k)
      qcl_old(l) = wtrac_pc2%qcl_old(i,j,k)
      qcl_change(l) = wtrac_pc2%q_cond(i,j,k)
      q_new(l)     = q_new_3d(i,j,k)
      qcl_new(l)   = qcl_new_3d(i,j,k)
    end do
  end do

  call wtrac_all_phase_chg(row_length*rows, n_wtrac,                           &
                       q_old, qcl_old, qcl_change,                             &
                       q_new, qcl_new,                                         &
                       'vap', 'liq', 'two_way',                                &
                       q_wtrac(:,:,k), qcl_wtrac(:,:,k),                       &
                       l_limit_chg=l_limit_chg,                                &
                       qchange_wtrac=qchange_wtrac(:,:,k))
end do
!$OMP end do

! Update main water tracer fields

select case (call_routine)

case ('pc2_initiate', 'pc2_checks2', 'pc2_hpt_press')
  ! Update main prognostic fields
!$OMP do SCHEDULE(STATIC)
  do k = 1, tdims%k_end
    do j = tdims%j_start, tdims%j_end                   ! No halos
      do i = tdims%i_start, tdims%i_end
        l = (j-1)*row_length + i
        do i_wt = 1, n_wtrac
          wtrac(i_wt)%q(i,j,k)   = q_wtrac(l,i_wt,k)
          wtrac(i_wt)%qcl(i,j,k) = qcl_wtrac(l,i_wt,k)
        end do
      end do
    end do
  end do
!$OMP end do

case ('mphys_turb')
  ! Update increments
!$OMP do SCHEDULE(STATIC)
  do k = 1, tdims%k_end
    do j = tdims%j_start, tdims%j_end                   ! No halos
      do i = tdims%i_start, tdims%i_end
        l = (j-1)*row_length + i
        do i_wt = 1, n_wtrac
          wtrac_as(i_wt)%q_star(i,j,k)   = wtrac_as(i_wt)%q_star(i,j,k)        &
                                           - qchange_wtrac(l,i_wt,k)
          wtrac_as(i_wt)%qcl_star(i,j,k) = wtrac_as(i_wt)%qcl_star(i,j,k)      &
                                           + qchange_wtrac(l,i_wt,k)
        end do
      end do
    end do
  end do
!$OMP end do

case ('pc2_bl_forced_cu')
  ! Update working arrays used in ni_imp_ctl
!$OMP do SCHEDULE(STATIC)
  do k = 1, tdims%k_end
    do j = tdims%j_start, tdims%j_end                   ! No halos
      do i = tdims%i_start, tdims%i_end
        l = (j-1)*row_length + i
        do i_wt = 1, n_wtrac
          wtrac_as(i_wt)%q_star(i,j,k)   = q_wtrac(l,i_wt,k)
          wtrac_as(i_wt)%qcl_star(i,j,k) = qcl_wtrac(l,i_wt,k)
        end do
      end do
    end do
  end do
!$OMP end do

case ('pc2_hpt_rad')
  ! Update increment fields
!$OMP do SCHEDULE(STATIC)
  do k = 1, tdims%k_end
    do j = tdims%j_start, tdims%j_end                   ! No halos
      do i = tdims%i_start, tdims%i_end
        l = (j-1)*row_length + i
        do i_wt = 1, n_wtrac
          wtrac_as(i_wt)%q_star(i,j,k)   = wtrac_as(i_wt)%q_star(i,j,k)        &
                     + q_wtrac(l,i_wt,k)   - wtrac(i_wt)%q(i,j,k)
          wtrac_as(i_wt)%qcl_star(i,j,k) = wtrac_as(i_wt)%qcl_star(i,j,k)      &
                     + qcl_wtrac(l,i_wt,k) - wtrac(i_wt)%qcl(i,j,k)
        end do
      end do
    end do
  end do
!$OMP end do

case DEFAULT
  ErrorStatus = 100
  cmessage =                                                                   &
    'Main arrays have not been updated due to invalid call_routine name'
  call ereport(RoutineName, ErrorStatus, cmessage)
end select

!$OMP end PARALLEL

! Deallocate working arrays
call wtrac_pc2_store_dealloc

if (lhook) call dr_hook(ModuleName//':'//RoutineName,zhook_out,zhook_handle)
return
end subroutine wtrac_pc2_phase_chg


end module wtrac_pc2_phase_chg_mod
